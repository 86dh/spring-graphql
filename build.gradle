import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
	id 'org.springframework.boot' version '2.4.0-SNAPSHOT' apply false
}

subprojects {
	apply plugin: 'java-library'
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'maven'

	group = 'org.springframework.experimental'
	version = "0.1.0-SNAPSHOT"
	sourceCompatibility = 1.8
	targetCompatibility = 1.8

	repositories {
		mavenCentral()
		maven { url 'https://repo.spring.io/milestone' }
		maven { url 'https://repo.spring.io/snapshot' }
	}

	dependencyManagement {
		imports {
			mavenBom SpringBootPlugin.BOM_COORDINATES
		}
		dependencies {
			dependency 'com.graphql-java:graphql-java:15.0'
		}
		generatedPomCustomization {
			enabled = false
		}
	}

	dependencies {
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
	}

	test {
		useJUnitPlatform()
		testLogging {
			events "passed", "skipped", "failed"
		}
	}

	apply from: "${rootDir}/gradle/publishing.gradle"
}
